{-# OPTIONS --safe #-}

module Cubical.Categories.Instances.Functors.More where

open import Cubical.Categories.Category renaming (isIso to isIsoC)
open import Cubical.Categories.Functor.Base
open import Cubical.Categories.NaturalTransformation.Base
open import Cubical.Categories.NaturalTransformation.Properties
open import Cubical.Categories.Morphism
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Equiv.Properties
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Categories.Instances.Functors
open import Cubical.Categories.Constructions.BinProduct
open import Cubical.Categories.Functor.Properties
open import Cubical.Categories.Equivalence.Base
open import Cubical.Categories.Category

open import Cubical.Tactics.CategorySolver.Reflection
open import Cubical.Categories.Equivalence.More

private
  variable
    â„“C â„“C' â„“D â„“D' â„“Î“ â„“Î“' â„“ â„“' : Level

module _ (C : Category â„“C â„“C') (D : Category â„“D â„“D') where
  open Category
  open Functor
  open NatTrans

  module _ {Î“ : Category â„“Î“ â„“Î“'} where
    -- The action of currying out the right argument of a Functor (Î“ Ã—C C) D
    Î»Fr : Functor (Î“ Ã—C C) D â†’ Functor Î“ (FUNCTOR C D)
    Î»Fr F .F-ob a .F-ob b = F âŸ… a , b âŸ†
    Î»Fr F .F-ob a .F-hom f = F .F-hom (Î“ .id , f)
    Î»Fr F .F-ob a .F-id = F .F-id
    Î»Fr F .F-ob a .F-seq f g =
      F .F-hom (Î“ .id , f â‹†âŸ¨ C âŸ© g)
        â‰¡âŸ¨ (Î» i â†’ F .F-hom ((Î“ .â‹†IdL (Î“ .id) (~ i)) , f â‹†âŸ¨ C âŸ© g)) âŸ©
      F .F-hom (Î“ .id â‹†âŸ¨ Î“ âŸ© Î“ .id , f â‹†âŸ¨ C âŸ© g)
        â‰¡âŸ¨ F .F-seq (Î“ .id , f) (Î“ .id , g) âŸ©
      F .F-hom (Î“ .id , f) â‹†âŸ¨ D âŸ© F .F-hom (Î“ .id , g ) âˆ
    Î»Fr F .F-hom Î³ .N-ob b = F .F-hom (Î³ , C .id)
    Î»Fr F .F-hom Î³ .N-hom f =
      F .F-hom (Î“ .id , f) â‹†âŸ¨ D âŸ© F .F-hom (Î³ , C .id)
        â‰¡âŸ¨ sym (F .F-seq (Î“ .id , f) (Î³ , C .id)) âŸ©
      F .F-hom (Î“ .id â‹†âŸ¨ Î“ âŸ© Î³ , f â‹†âŸ¨ C âŸ© C .id)
        â‰¡âŸ¨ (Î» i â†’ F .F-hom ((idTrans (Id {C = Î“}) .N-hom Î³ (~ i)) ,
                             idTrans (Id {C = C}) .N-hom f i)) âŸ©
      F .F-hom (Î³ â‹†âŸ¨ Î“ âŸ© Î“ .id , C .id â‹†âŸ¨ C âŸ© f)
        â‰¡âŸ¨ F .F-seq (Î³ , C .id) (Î“ .id , f) âŸ©
      F .F-hom (Î³ , C .id) â‹†âŸ¨ D âŸ© F .F-hom (Î“ .id , f)  âˆ
    Î»Fr F .F-id = makeNatTransPath (funExt (Î» a â†’ F .F-id))
    Î»Fr F .F-seq Î³ Î´ = makeNatTransPath (funExt (Î» a â†’
        F .F-hom (Î³ â‹†âŸ¨ Î“ âŸ© Î´ , C .id)
          â‰¡âŸ¨ (Î» i â†’ F .F-hom (Î³ â‹†âŸ¨ Î“ âŸ© Î´ , C .â‹†IdL (C .id) (~ i))) âŸ©
        F .F-hom (Î³ â‹†âŸ¨ Î“ âŸ© Î´ , C .id â‹†âŸ¨ C âŸ© C .id)
          â‰¡âŸ¨ F .F-seq (Î³ , C .id) (Î´ , C .id) âŸ©
        F .F-hom (Î³ , C .id) â‹†âŸ¨ D âŸ© F .F-hom (Î´ , C .id) âˆ))

    -- Functorially extend the currying action from a
    -- function on objects to a functor between
    -- the relevant functor categories
    -- Here "currying" pulls out the right argument.
    -- We will define a similar left-sided version
    -- under the name curryFl
    curryF : Functor (FUNCTOR (Î“ Ã—C C) D) (FUNCTOR Î“ (FUNCTOR C D))
    curryF .F-ob = Î»Fr
    curryF .F-hom Î· .N-ob Î³ .N-ob c = Î· .N-ob (Î³ , c)
    curryF .F-hom Î· .N-ob Î³ .N-hom Ï• = Î· .N-hom (Î“ .id , Ï•)
    curryF .F-hom Î· .N-hom f =
      makeNatTransPath (funExt (Î» (c : C .ob) â†’ Î· .N-hom (f , C .id)))
    curryF .F-id = makeNatTransPath (funExt Î» (Î³ : Î“ .ob) â†’ refl)
    curryF .F-seq Î· Î·' = makeNatTransPath (funExt Î» (Î³ : Î“ .ob) â†’ refl)

    -- Preimage for the ESO proof
    -- i.e. an object in (FUNCTOR Î“ (FUNCTOR C D)) that maps to Î»F
    curryF-ESO-object-preimage : (FUNCTOR Î“ (FUNCTOR C D)) .ob â†’
                                 (FUNCTOR (Î“ Ã—C C) D) .ob
    curryF-ESO-object-preimage Î»F .F-ob (Î³ , c) =  Î»F .F-ob Î³ .F-ob c
    curryF-ESO-object-preimage Î»F .F-hom
      {x = (Î³â‚ , câ‚)} {y = (Î³â‚‚ , câ‚‚)} (Ï• , Ïˆ) =
        Î»F .F-hom Ï• .N-ob câ‚ â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚‚ .F-hom Ïˆ
    curryF-ESO-object-preimage Î»F .F-seq
      {x = (Î³â‚ , câ‚)} {y = (Î³â‚‚ , câ‚‚)} {z = (Î³â‚ƒ , câ‚ƒ)} (Ï•â‚ , Ïˆâ‚) (Ï•â‚‚ , Ïˆâ‚‚) = (
      curryF-ESO-object-preimage Î»F .F-hom ((Ï•â‚ , Ïˆâ‚) â‹†âŸ¨ Î“ Ã—C C âŸ© (Ï•â‚‚ , Ïˆâ‚‚))
        â‰¡âŸ¨ ((Î» i â†’ ( (Î»F .F-seq Ï•â‚ Ï•â‚‚ (i) ) .N-ob câ‚ â‹†âŸ¨ D âŸ©
          Î»F .F-ob Î³â‚ƒ .F-hom (Ïˆâ‚ â‹†âŸ¨ C âŸ© Ïˆâ‚‚)))) âŸ©
      (Î»F .F-hom Ï•â‚ â‹†âŸ¨ FUNCTOR C D âŸ© Î»F .F-hom Ï•â‚‚) .N-ob câ‚ â‹†âŸ¨ D âŸ©
        Î»F .F-ob Î³â‚ƒ .F-hom (Ïˆâ‚ â‹†âŸ¨ C âŸ© Ïˆâ‚‚)
        â‰¡âŸ¨ (Î» i â†’ (Î»F .F-hom Ï•â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ©
           Î»F .F-hom Ï•â‚‚ .N-ob câ‚ â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚ƒ .F-seq Ïˆâ‚ Ïˆâ‚‚ i)) âŸ©
      (Î»F .F-hom Ï•â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ©
        Î»F .F-hom Ï•â‚‚ .N-ob câ‚) â‹†âŸ¨ D âŸ©
          (Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚ â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚‚)
        â‰¡âŸ¨ solveCat! D âŸ©
      Î»F .F-hom Ï•â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ© (Î»F .F-hom Ï•â‚‚ .N-ob câ‚ â‹†âŸ¨ D âŸ©
        Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚) â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚‚
        â‰¡âŸ¨ ((Î» i â†’ ( Î»F .F-hom Ï•â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ©
          (Î»F .F-hom Ï•â‚‚ .N-hom Ïˆâ‚ (~ i) ) â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚‚ ))) âŸ©
      Î»F .F-hom Ï•â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ© (Î»F .F-ob Î³â‚‚ .F-hom Ïˆâ‚ â‹†âŸ¨ D âŸ©
        Î»F .F-hom Ï•â‚‚ .N-ob câ‚‚) â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚ƒ .F-hom Ïˆâ‚‚
        â‰¡âŸ¨ solveCat! D âŸ©
      curryF-ESO-object-preimage Î»F .F-hom (Ï•â‚ , Ïˆâ‚) â‹†âŸ¨ D âŸ©
        curryF-ESO-object-preimage Î»F .F-hom (Ï•â‚‚ , Ïˆâ‚‚) âˆ)
    curryF-ESO-object-preimage Î»F .F-id  {x = (Î³ , c)} = (
      curryF-ESO-object-preimage Î»F .F-hom (Î“ .id , C .id)
        â‰¡âŸ¨ ((Î» i â†’ (Î»F .F-id i .N-ob c â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom (C .id)))) âŸ©
      D .id â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom (C .id)
        â‰¡âŸ¨ ((Î» i â†’ (D .id â‹†âŸ¨ D âŸ© (Î»F .F-ob Î³ .F-id i)))) âŸ©
      D .id â‹†âŸ¨ D âŸ© D .id
        â‰¡âŸ¨ D .â‹†IdL (D .id) âŸ©
      D .id
      âˆ )
    -- Half of the isomorphism between (curryF-ESO-object-preimage Î»F) and Î»F
    curryF-ESO-morphism-preimage : (Î»F : (FUNCTOR Î“ (FUNCTOR C D)) .ob) â†’
                                   NatTrans
                                     (curryF .F-ob
                                       (curryF-ESO-object-preimage Î»F))
                                     Î»F
    curryF-ESO-morphism-preimage Î»F .N-ob Î³ .N-ob c = D .id
    curryF-ESO-morphism-preimage Î»F .N-ob Î³ .N-hom {x = câ‚} {y = câ‚‚} Ïˆ =
      ((Î»F .F-hom (Î“ .id) .N-ob câ‚) â‹†âŸ¨ D âŸ© (Î»F .F-ob Î³ .F-hom Ïˆ)) â‹†âŸ¨ D âŸ© D .id
        â‰¡âŸ¨ (Î» i â†’ (D .â‹†IdR ((Î»F .F-hom (Î“ .id) .N-ob câ‚) â‹†âŸ¨ D âŸ©
          (Î»F .F-ob Î³ .F-hom Ïˆ)) (i) )) âŸ©
      (Î»F .F-hom (Î“ .id) .N-ob câ‚) â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom Ïˆ
        â‰¡âŸ¨ ((Î» i â†’ ((Î»F .F-id i) .N-ob câ‚ â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom Ïˆ ))) âŸ©
      D .id â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom Ïˆ âˆ
    curryF-ESO-morphism-preimage Î»F .N-hom {x = Î³â‚} {y = Î³â‚‚} Ï• =
      makeNatTransPath (funExt (Î» (c : C .ob) â†’
      curryF .F-ob (curryF-ESO-object-preimage Î»F) .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ©
        (curryF-ESO-morphism-preimage Î»F) .N-ob Î³â‚‚ .N-ob c
        â‰¡âŸ¨ D .â‹†IdR
          (curryF .F-ob (curryF-ESO-object-preimage Î»F) .F-hom Ï• .N-ob c) âŸ©
      Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚‚ .F-hom (C .id)
        â‰¡âŸ¨ ((Î» i â†’ (Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚‚ .F-id i))) âŸ©
      Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ© D .id
        â‰¡âŸ¨ D .â‹†IdR (Î»F .F-hom Ï• .N-ob c) âŸ©
      Î»F .F-hom Ï• .N-ob c
        â‰¡âŸ¨ ((Î» i â†’ (D .â‹†IdL (Î»F .F-hom Ï• .N-ob c) (~ i) ))) âŸ©
      (curryF-ESO-morphism-preimage Î»F) .N-ob Î³â‚ .N-ob c â‹†âŸ¨ D âŸ©
        Î»F .F-hom Ï• .N-ob c âˆ))

    open isIso

    -- Show that curryF-ESO-morphism-preimage is indeed an isomorphism in
    -- FUNCTOR Î“ (FUNCTOR C D)
    curryF-ESO-morphism-preimage-isIso : (Î»F : (FUNCTOR Î“ (FUNCTOR C D)) .ob) â†’
                                         isIsoC
                                           (FUNCTOR Î“ (FUNCTOR C D))
                                           (curryF-ESO-morphism-preimage Î»F)
    curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-ob c =  D .id
    curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-hom
      {x = câ‚} {y = câ‚‚} Ïˆ =
      Î»F .F-ob Î³ .F-hom Ïˆ â‹†âŸ¨ D âŸ© D .id
        â‰¡âŸ¨ D .â‹†IdR (Î»F .F-ob Î³ .F-hom Ïˆ) âŸ©
      Î»F .F-ob Î³ .F-hom Ïˆ
        â‰¡âŸ¨ (Î» i â†’ (D .â‹†IdL (Î»F .F-ob Î³ .F-hom Ïˆ) (~ i))) âŸ©
      D .id â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom Ïˆ
        â‰¡âŸ¨ ((Î» i â†’ (Î»F .F-id (~ i)) .N-ob câ‚ â‹†âŸ¨ D âŸ© Î»F .F-ob Î³ .F-hom Ïˆ ) ) âŸ©
     curryF .F-ob (curryF-ESO-object-preimage Î»F) .F-ob Î³ .F-hom Ïˆ
        â‰¡âŸ¨ ((Î» i â†’ (D .â‹†IdL
          (curryF .F-ob (curryF-ESO-object-preimage Î»F) .F-ob Î³ .F-hom Ïˆ )
            (~ i) ))) âŸ©
      D .id â‹†âŸ¨ D âŸ©
        curryF .F-ob (curryF-ESO-object-preimage Î»F) .F-ob Î³ .F-hom Ïˆ âˆ
    curryF-ESO-morphism-preimage-isIso Î»F .inv .N-hom {x = Î³â‚} {y = Î³â‚‚} Ï• =
      makeNatTransPath (funExt (Î» (c : C .ob ) â†’
      Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ©
        curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³â‚‚ .N-ob c
        â‰¡âŸ¨ D .â‹†IdR (Î»F .F-hom Ï• .N-ob c) âŸ©
      Î»F .F-hom Ï• .N-ob c
        â‰¡âŸ¨ ((Î» i â†’ (D .â‹†IdR (Î»F .F-hom Ï• .N-ob c) (~ i)))) âŸ©
      Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ© D .id
        â‰¡âŸ¨ ((Î» i â†’ ( Î»F .F-hom Ï• .N-ob c â‹†âŸ¨ D âŸ© Î»F .F-ob Î³â‚‚ .F-id (~ i )))) âŸ©
      Î»Fr (curryF-ESO-object-preimage Î»F) .F-hom Ï• .N-ob c
        â‰¡âŸ¨ ((Î» i â†’ ((D .â‹†IdL (Î»Fr (curryF-ESO-object-preimage Î»F)
          .F-hom Ï• .N-ob c) (~ i)) ))) âŸ©
      D .id â‹†âŸ¨ D âŸ© Î»Fr (curryF-ESO-object-preimage Î»F) .F-hom Ï• .N-ob c âˆ
      ))
    curryF-ESO-morphism-preimage-isIso Î»F .sec =
      makeNatTransPath (funExt (Î» (Î³ : Î“ .ob) â†’
      makeNatTransPath (funExt (Î» (c : C .ob) â†’
        curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-ob c â‹†âŸ¨ D âŸ©
          curryF-ESO-morphism-preimage Î»F .N-ob Î³ .N-ob c
          â‰¡âŸ¨ D .â‹†IdR
            (curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-ob c) âŸ©
        D .id âˆ
      ))))
    curryF-ESO-morphism-preimage-isIso Î»F .ret =
      makeNatTransPath (funExt (Î» (Î³ : Î“ .ob) â†’
      makeNatTransPath (funExt (Î» (c : C .ob) â†’
         curryF-ESO-morphism-preimage Î»F .N-ob Î³ .N-ob c â‹†âŸ¨ D âŸ©
           curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-ob c
          â‰¡âŸ¨ D .â‹†IdR
            (curryF-ESO-morphism-preimage-isIso Î»F .inv .N-ob Î³ .N-ob c) âŸ©
        D .id âˆ
      ))))


    -- to prove that curryF is an equivalence,
    -- we construct the inverse functor, uncurryF
    uncurryF : Functor (FUNCTOR Î“ (FUNCTOR C D)) (FUNCTOR (Î“ Ã—C C) D)
    uncurryF .F-ob Î»F = curryF-ESO-object-preimage Î»F
    -- stolen from curryF-full-preimage
    uncurryF .F-hom {F} {G} Î»Î· .N-ob (Î³ , c) = Î»Î· .N-ob Î³ .N-ob c
    uncurryF .F-hom {F} {G} Î»Î· .N-hom {(Î³â‚ , câ‚)} {(Î³â‚‚ , câ‚‚)} (Ï•â‚ , Ï•â‚‚) =
      uncurryF .F-ob F .F-hom (Ï•â‚ , Ï•â‚‚) â‹†âŸ¨ D âŸ©
        uncurryF .F-hom Î»Î· .N-ob (Î³â‚‚ , câ‚‚)
        â‰¡âŸ¨ solveCat! D âŸ©
      F .F-hom (Ï•â‚) .N-ob câ‚ â‹†âŸ¨ D âŸ© (F .F-ob Î³â‚‚ .F-hom Ï•â‚‚ â‹†âŸ¨ D âŸ©
        Î»Î· .N-ob Î³â‚‚ .N-ob câ‚‚)
        â‰¡âŸ¨ (Î» i â†’ (F .F-hom (Ï•â‚) .N-ob câ‚ â‹†âŸ¨ D âŸ© (Î»Î· .N-ob Î³â‚‚ .N-hom Ï•â‚‚ (i)))) âŸ©
      F .F-hom (Ï•â‚) .N-ob câ‚ â‹†âŸ¨ D âŸ©
        (Î»Î· .N-ob Î³â‚‚ .N-ob câ‚ â‹†âŸ¨ D âŸ© G .F-ob Î³â‚‚ .F-hom Ï•â‚‚)
        â‰¡âŸ¨ solveCat! D âŸ©
      (F .F-hom (Ï•â‚) .N-ob câ‚ â‹†âŸ¨ D âŸ©
        Î»Î· .N-ob Î³â‚‚ .N-ob câ‚) â‹†âŸ¨ D âŸ© G .F-ob Î³â‚‚ .F-hom Ï•â‚‚
       â‰¡âŸ¨ (Î» i â†’ (((Î»Î· .N-hom (Ï•â‚) (i)) .N-ob câ‚) â‹†âŸ¨ D âŸ©
         G .F-ob Î³â‚‚ .F-hom Ï•â‚‚)) âŸ©
      (Î»Î· .N-ob Î³â‚ .N-ob câ‚ â‹†âŸ¨ D âŸ© G .F-hom (Ï•â‚) .N-ob câ‚) â‹†âŸ¨ D âŸ©
        G .F-ob Î³â‚‚ .F-hom Ï•â‚‚
        â‰¡âŸ¨ solveCat! D âŸ©
      uncurryF .F-hom Î»Î· .N-ob (Î³â‚ , câ‚) â‹†âŸ¨ D âŸ©
        uncurryF .F-ob G .F-hom (Ï•â‚ , Ï•â‚‚) âˆ
    uncurryF .F-id = makeNatTransPath (funExt (Î» x â†’ refl ))
    uncurryF .F-seq Î»Î·â‚ Î»Î·â‚‚ = makeNatTransPath (funExt (Î» x â†’ refl ))


    open isEquivalence
    open NatIso

    -- curryF is an equivalence. Done using Î· Îµ isos constructed explicitly.
    -- most of the time, these are the identity
    curryF-isEquivalence : isEquivalence curryF
    curryF-isEquivalence =
      record { invFunc = uncurryF ; Î· = Î·-iso ; Îµ = Îµ-iso } where
      -- separate definition to sidestep Agda termination issue
      Î·-trans : NatTrans ğŸ™âŸ¨ FUNCTOR (Î“ Ã—C C) D âŸ© (uncurryF âˆ˜F curryF)
      Î·-trans .N-ob F .N-ob (Î³ , c) = D .id
      Î·-trans .N-ob F .N-hom {(Î³â‚ , câ‚)} {(Î³â‚‚ , câ‚‚)} (Ï•â‚ , Ï•â‚‚) =
        (F .F-hom (Ï•â‚ , Ï•â‚‚)) â‹†âŸ¨ D âŸ© D .id
          â‰¡âŸ¨ (Î» i â†’
            (F .F-hom ((Î“ .â‹†IdR Ï•â‚) (~ i) , (C .â‹†IdL Ï•â‚‚) (~ i)) â‹†âŸ¨ D âŸ© D .id)) âŸ©
        (F .F-hom ((Ï•â‚ , C .id) â‹†âŸ¨ Î“ Ã—C C âŸ© (Î“ .id , Ï•â‚‚))) â‹†âŸ¨ D âŸ© D .id
          â‰¡âŸ¨ (Î» i â†’ (F .F-seq (Ï•â‚ , C .id) (Î“ .id , Ï•â‚‚)) (i) â‹†âŸ¨ D âŸ© D .id) âŸ©
        (F .F-hom (Ï•â‚ , C .id) â‹†âŸ¨ D âŸ© F .F-hom (Î“ .id , Ï•â‚‚)) â‹†âŸ¨ D âŸ© D .id
          â‰¡âŸ¨ solveCat! D âŸ©
        D .id â‹†âŸ¨ D âŸ© ((uncurryF âˆ˜F curryF) .F-ob F) .F-hom (Ï•â‚ , Ï•â‚‚)  âˆ
      Î·-trans .N-hom {F} {G} Î² =
        makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))

      Î·-iso : NatIso ğŸ™âŸ¨ FUNCTOR (Î“ Ã—C C) D âŸ© (uncurryF âˆ˜F curryF)
      Î·-iso .trans = Î·-trans

      Î·-iso .nIso F .inv .N-ob (Î³ , c) = D .id
      Î·-iso .nIso F .inv .N-hom {(Î³â‚ , câ‚)} {(Î³â‚‚ , câ‚‚)} (Ï•â‚ , Ï•â‚‚) =
        ((uncurryF âˆ˜F curryF) .F-ob F) .F-hom (Ï•â‚ , Ï•â‚‚) â‹†âŸ¨ D âŸ© D .id
          â‰¡âŸ¨ solveCat! D âŸ©
        D .id â‹†âŸ¨ D âŸ© ((uncurryF âˆ˜F curryF) .F-ob F) .F-hom (Ï•â‚ , Ï•â‚‚)
          â‰¡âŸ¨ sym (Î·-iso .trans .N-ob F .N-hom (Ï•â‚ , Ï•â‚‚)) âŸ©
        (F .F-hom (Ï•â‚ , Ï•â‚‚)) â‹†âŸ¨ D âŸ© D .id
          â‰¡âŸ¨ solveCat! D âŸ©
        D .id â‹†âŸ¨ D âŸ© (F .F-hom (Ï•â‚ , Ï•â‚‚))  âˆ
      Î·-iso .nIso F .sec = makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))
      Î·-iso .nIso F .ret = makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))

      Îµ-iso : NatIso (curryF âˆ˜F uncurryF) ğŸ™âŸ¨ FUNCTOR Î“ (FUNCTOR C D) âŸ©
      Îµ-iso .trans .N-ob Î»F = curryF-ESO-morphism-preimage Î»F
      Îµ-iso .trans .N-hom {Î»F} {Î»G} Î»Î² = makeNatTransPath (funExt (Î» Î³ â†’
        makeNatTransPath (funExt (Î» c â†’
          -- TODO: For some reason this doesn't simplify to just solvecat...
          (Î»Î² .N-ob Î³ .N-ob c) â‹†âŸ¨ D âŸ© D .id
            â‰¡âŸ¨ solveCat! D âŸ©
          D .id â‹†âŸ¨ D âŸ© Î»Î² .N-ob Î³ .N-ob c âˆ ))))
      Îµ-iso .nIso = (Î» Î»F â†’ curryF-ESO-morphism-preimage-isIso Î»F)


    open Cubical.Categories.Equivalence.Base._â‰ƒá¶œ_

    curryEquivalence : FUNCTOR (Î“ Ã—C C) D â‰ƒá¶œ FUNCTOR Î“ (FUNCTOR C D)
    curryEquivalence .func = curryF
    curryEquivalence ._â‰ƒá¶œ_.isEquiv = curryF-isEquivalence

    -- We also want a notion of currying out the left argument.
    -- We do this by composing
    -- a swapping functor with the right-sided currying functor
    -- To show that this left-handed currying is also an equivalence,
    -- we will need to show that
    -- the swapping functor is an equivalence
    swapArgs : Functor (FUNCTOR (C Ã—C Î“) D) (FUNCTOR (Î“ Ã—C C) D)
    swapArgs .F-ob F .F-ob (c , Î³) = F .F-ob (Î³ , c)
    swapArgs .F-ob F .F-hom (Ïˆ , Ï•) = F .F-hom (Ï• , Ïˆ)
    swapArgs .F-ob F .F-id = F .F-id
    swapArgs .F-ob F .F-seq (Ïˆâ‚ , Ï•â‚) (Ïˆâ‚‚ , Ï•â‚‚) = F .F-seq (Ï•â‚ , Ïˆâ‚) (Ï•â‚‚ , Ïˆâ‚‚)
    swapArgs .F-hom Î· .N-ob (Î³ , c) = Î· .N-ob (c , Î³)
    swapArgs .F-hom Î· .N-hom (Ï• , Ïˆ) = Î· .N-hom (Ïˆ , Ï•)
    swapArgs .F-id = makeNatTransPath (funExt Î» (Î³ , c) â†’ refl)
    swapArgs .F-seq Î· Î·' = makeNatTransPath (funExt Î» (Î³ , c) â†’ refl)

    swapArgs-inv : Functor (FUNCTOR (Î“ Ã—C C) D) (FUNCTOR (C Ã—C Î“) D)
    swapArgs-inv .F-ob F .F-ob (Î³ , c) = F .F-ob (c , Î³)
    swapArgs-inv .F-ob F .F-hom (Ï• , Ïˆ) = F .F-hom (Ïˆ , Ï•)
    swapArgs-inv .F-ob F .F-id = F .F-id
    swapArgs-inv .F-ob F .F-seq (Ï•â‚ , Ïˆâ‚) (Ï•â‚‚ , Ïˆâ‚‚) =
      F .F-seq (Ïˆâ‚ , Ï•â‚) (Ïˆâ‚‚ , Ï•â‚‚)
    swapArgs-inv .F-hom Î· .N-ob (Î³ , c) = Î· .N-ob (c , Î³)
    swapArgs-inv .F-hom Î· .N-hom (Ïˆ , Ï•) = Î· .N-hom (Ï• , Ïˆ)
    swapArgs-inv .F-id = makeNatTransPath (funExt Î» (Î³ , c) â†’ refl)
    swapArgs-inv .F-seq Î· Î·' = makeNatTransPath (funExt Î» (Î³ , c) â†’ refl)

    swapArgs-isEquivalence : isEquivalence swapArgs
    swapArgs-isEquivalence =
      record { invFunc = swapArgs-inv ; Î· = the-Î· ; Îµ = the-Îµ } where
      Î·-morphisms : N-ob-Type ğŸ™âŸ¨ FUNCTOR (C Ã—C Î“) D âŸ©
                              (funcComp swapArgs-inv swapArgs)
      Î·-morphisms F .N-ob Î³ = D .id
      Î·-morphisms F .N-hom Ï• = solveCat! D

      the-Î· : NatIso ğŸ™âŸ¨ FUNCTOR (C Ã—C Î“) D âŸ© (funcComp swapArgs-inv swapArgs)
      the-Î· .trans .N-ob = Î·-morphisms
      the-Î· .trans .N-hom Î± =
        makeNatTransPath (funExt (Î» (c , Î³) â†’ solveCat! D))
      the-Î· .nIso F .inv .N-ob (c , Î³) = D .id
      the-Î· .nIso F .inv .N-hom (Ïˆ , Ï•) = solveCat! D
      the-Î· .nIso F .sec = makeNatTransPath (funExt (Î» (c , Î³) â†’ solveCat! D))
      the-Î· .nIso F .ret = makeNatTransPath (funExt (Î» (c , Î³) â†’ solveCat! D))

      Îµ-morphisms : N-ob-Type (funcComp swapArgs swapArgs-inv)
                              ğŸ™âŸ¨ FUNCTOR (Î“ Ã—C C) D âŸ©
      Îµ-morphisms F .N-ob c = D .id
      Îµ-morphisms F .N-hom Ïˆ = solveCat! D

      the-Îµ : NatIso (funcComp swapArgs swapArgs-inv) ğŸ™âŸ¨ FUNCTOR (Î“ Ã—C C) D âŸ©
      the-Îµ .trans .N-ob = Îµ-morphisms
      the-Îµ .trans .N-hom Î± =
        makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))
      the-Îµ .nIso F .inv .N-ob (Î³ , c) = D .id
      the-Îµ .nIso F .inv .N-hom (Ï† , Ïˆ) = solveCat! D
      the-Îµ .nIso F .sec = makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))
      the-Îµ .nIso F .ret = makeNatTransPath (funExt (Î» (Î³ , c) â†’ solveCat! D))

    curryFl : Functor (FUNCTOR (C Ã—C Î“) D) (FUNCTOR Î“ (FUNCTOR C D))
    curryFl = curryF âˆ˜F swapArgs


    curryFl-isEquivalence : isEquivalence curryFl
    curryFl-isEquivalence =
      isEquivalenceComp swapArgs-isEquivalence curryF-isEquivalence
