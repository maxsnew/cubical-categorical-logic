{-# OPTIONS --safe #-}

module Cubical.Categories.Equivalence.More where

open import Cubical.Categories.Category
open import Cubical.Categories.Functor.Base
open import Cubical.Categories.NaturalTransformation.Base
open import Cubical.Categories.NaturalTransformation.Properties
open import Cubical.Categories.NaturalTransformation.More
open import Cubical.Foundations.Prelude
open import Cubical.Categories.Equivalence.Base



private
  variable
    â„“C â„“C' â„“D â„“D' â„“E â„“E' : Level


module _
  {C : Category â„“C â„“C'} {D : Category â„“D â„“D'} {E : Category â„“E â„“E'}
  {F : Functor C D} {G : Functor D E}
  where

  open Category
  open Functor
  open NatTrans
  open isEquivalence
  open NatIso
  open isIso

  isEquivalenceComp : isEquivalence F â†’ isEquivalence G â†’ isEquivalence (G âˆ˜F F)
  isEquivalenceComp Feq Geq = record { invFunc = F'âˆ˜G' ;
                                       Î· = Î·-iso ;
                                       Îµ = Îµ-iso } where
    F'âˆ˜G' : Functor E C
    F'âˆ˜G' = Feq .invFunc âˆ˜F  Geq .invFunc
    Î·-iso : NatIso ğŸ™âŸ¨ C âŸ© (F'âˆ˜G' âˆ˜F (G âˆ˜F F))
    Î·-iso = seqNatIso
      -- proof that 1 and (F' F) are iso
      (Feq .Î·)
      -- proof that (F' F) and (F' G') (G F) are iso
      (seqNatIso
        -- precompose nested iso with F'
        ((Feq .invFunc) âˆ˜Ê³i seqNatIso
          -- proof that F and (G' G) F are isomorphic
          (seqNatIso
            -- proof that F and 1 F are iso
            (symNatIso (CATâ‹†IdR {F = F}))
            -- proof that 1 F and (G' G) F are iso (whisker with F)
            (F âˆ˜Ë¡i (Geq .Î·)))
          -- associate the parentheses (G' G) F and G' (G F)
          (symNatIso (CATâ‹†Assoc F G (Geq .invFunc)))
        )
        -- fix final assoc F' (G' (G F)) iso to (F' G') (G F)
        (CATâ‹†Assoc (G âˆ˜F F) (Geq .invFunc) (Feq .invFunc))
      )

    Îµ-iso : NatIso ((G âˆ˜F F) âˆ˜F F'âˆ˜G') ğŸ™âŸ¨ E âŸ©
    Îµ-iso = seqNatIso
      -- proof that (G F) (F' G') and G G' are iso
      (seqNatIso
        -- proof that (G F) (F' G') and G (F (F' G')) are iso
        (symNatIso (CATâ‹†Assoc (F'âˆ˜G') F G))
        -- post compose nested proof with G
        (G âˆ˜Ê³i seqNatIso
          -- proof that F (F' G') and 1 G' are iso
          (seqNatIso
            -- proof that F (F' G') and (F F') G' are iso
            (CATâ‹†Assoc (Geq .invFunc) (Feq .invFunc) F)
            -- proof that (F F') G' and 1 G' are iso (whisker with G')
            ((Geq .invFunc) âˆ˜Ë¡i (Feq .Îµ))
          )
          -- proof that (1 G') and G are iso
          (CATâ‹†IdR {F = Geq .invFunc})
        )
      )
      -- proof that G G' and 1 are iso
      (Geq .Îµ)
