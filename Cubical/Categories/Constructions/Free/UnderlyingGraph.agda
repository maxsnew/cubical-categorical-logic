{-# OPTIONS --safe #-}

module Cubical.Categories.Constructions.Free.UnderlyingGraph where

open import Cubical.Categories.Morphism
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Categories.Category.Base
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation hiding (_âŸ¦_âŸ§)
open import Cubical.Categories.NaturalTransformation.More
open import Cubical.Data.Graph.Base
open import Cubical.Categories.Isomorphism

open import Cubical.Data.Graph.Properties

private
  variable
    â„“c â„“c' â„“d â„“d' â„“e â„“e' â„“g â„“g' â„“h â„“h' : Level

open Category
open isIso
open Functor
open NatIso hiding (sqRL; sqLL)
open NatTrans

-- Underlying graph of a category
Ugr : âˆ€ {â„“c â„“c'} (ğ“’ : Category â„“c â„“c') â†’ Graph â„“c â„“c'
Ugr ğ“’ .Node = ğ“’ .ob
Ugr ğ“’ .Edge = ğ“’ .Hom[_,_]

Uhom : âˆ€ {â„“c â„“c' â„“d â„“d'} {ğ“’ : Category â„“c â„“c'} {ğ““ : Category â„“d â„“d'} (F : Functor ğ“’ ğ““)
     â†’ GraphHom (Ugr ğ“’) (Ugr ğ““)
Uhom F ._$g_ = Functor.F-ob F
Uhom F ._<$g>_ = Functor.F-hom F

module _ (G : Graph â„“g â„“g') (ğ“’ : Category â„“c â„“c') where
  Interp : Type _
  Interp = GraphHom G (Ugr ğ“’)

  InterpTrans : Interp â†’ Interp â†’ Type _
  InterpTrans Ä± Ä±' =
    Î£[ f âˆˆ (âˆ€ v â†’ ğ“’ [ Ä± $g v , Ä±' $g v ])]
    (âˆ€ {v}{w}(e : G .Edge v w) â†’ f w âˆ˜âŸ¨ ğ“’ âŸ© (Ä± <$g> e) â‰¡ Ä±' <$g> e âˆ˜âŸ¨ ğ“’ âŸ© f v)

  isInterpIso : {Ä± Ä±' : Interp} â†’ InterpTrans Ä± Ä±' â†’ Type (â„“-max â„“g â„“c')
  isInterpIso Î± = âˆ€ v â†’ isIso ğ“’ (Î± .fst v)

  InterpIso : Interp â†’ Interp â†’ Type _
  InterpIso Ä± Ä±' = Î£ (InterpTrans Ä± Ä±') isInterpIso

  idInterpIso : {Ä± : Interp} â†’ InterpIso Ä± Ä±
  idInterpIso .fst .fst v = ğ“’ .id
  idInterpIso .fst .snd e = ğ“’ .â‹†IdR _ âˆ™ sym (ğ“’ .â‹†IdL _)
  idInterpIso .snd v = idCatIso .snd

  -- if ğ“’ is univalent, this should be an equivalence
  pathToInterpIso : {Ä± Ä±' : Interp} â†’ Ä± â‰¡ Ä±' â†’ InterpIso Ä± Ä±'
  pathToInterpIso {Ä±}{Ä±'} p = transport (Î» i â†’ InterpIso Ä± (p i)) idInterpIso

  isIsoInterpIso : {Ä± Ä±' : Interp} (Î± : InterpIso Ä± Ä±') â†’ âˆ€ v â†’ CatIso ğ“’ (Ä± $g v) (Ä±' $g v)
  isIsoInterpIso Î± v = (Î± .fst .fst v) , (Î± .snd v)

  _âˆ˜InterpIso_ : {Ä± Ä±' Ä±'' : Interp} â†’ InterpIso Ä±' Ä±'' â†’ InterpIso Ä± Ä±' â†’ InterpIso Ä± Ä±''
  (Î± âˆ˜InterpIso Î²) .fst .fst v = Î± .fst .fst v âˆ˜âŸ¨ ğ“’ âŸ© Î² .fst .fst v
  (Î± âˆ˜InterpIso Î²) .fst .snd e =
    sym (ğ“’ .â‹†Assoc _ _ _)
    âˆ™ cong (comp' ğ“’ (Î± .fst .fst _)) (Î² .fst .snd e)
    âˆ™ ğ“’ .â‹†Assoc _ _ _
    âˆ™ cong (seq' ğ“’ (Î² .fst .fst _)) (Î± .fst .snd e)
    âˆ™ sym (ğ“’ .â‹†Assoc _ _ _)

  (Î± âˆ˜InterpIso Î²) .snd v = compIso (isIsoInterpIso Î± v) (isIsoInterpIso Î² v) .snd

  module InterpReasoning (Ä± : Interp) (Ä±' : Interp) (Î± : InterpIso Ä± Ä±') where
    open isIso
    sqRL : âˆ€ {v w} â†’ {e : G .Edge v w}
         â†’ Ä± <$g> e â‰¡ Î± .fst .fst v â‹†âŸ¨ ğ“’ âŸ© Ä±' <$g> e â‹†âŸ¨ ğ“’ âŸ© Î± .snd w .inv
    sqRL {v}{w}{e} = invMoveR (isIsoâ†’areInv (Î± .snd w)) (Î± .fst .snd e)

    -- copied from NaturalTransformation.Base
    sqLL : âˆ€ {v w} â†’ {e : G .Edge v w}
         â†’ Ä±' <$g> e â‹†âŸ¨ ğ“’ âŸ© Î± .snd w .inv â‰¡ Î± .snd v .inv â‹†âŸ¨ ğ“’ âŸ© (Ä± <$g> e)
    sqLL {v}{w}{e} = invMoveL (isIsoâ†’areInv (Î± .snd v)) (sym (sqRL âˆ™ ğ“’ .â‹†Assoc _ _ _))

symInterpIso : âˆ€ {G : Graph â„“g â„“g'} {ğ“’ : Category â„“c â„“c'}
             â†’ {Ä± Ä±' : Interp G ğ“’}
             â†’ InterpIso G ğ“’ Ä± Ä±'
             â†’ InterpIso G ğ“’ Ä±' Ä±
symInterpIso Î± .fst .fst v = Î± .snd v .inv
symInterpIso Î± .fst .snd e = InterpReasoning.sqLL _ _ _ _ Î±
symInterpIso Î± .snd v .inv = Î± .fst .fst v
symInterpIso Î± .snd v .sec = Î± .snd v .ret
symInterpIso Î± .snd v .ret = Î± .snd v .sec

_â‹†Interp_ : âˆ€ {G : Graph â„“g â„“g'}
              {ğ“’ : Category â„“c â„“c'}
              {ğ““ : Category â„“d â„“d'}
              (Ä± : Interp G ğ“’)
              (F : Functor ğ“’ ğ““)
              â†’ Interp G ğ““
(Ä± â‹†Interp F) ._$g_ x = Functor.F-ob F (Ä± $g x)
(Ä± â‹†Interp F) ._<$g>_ e = Functor.F-hom F (Ä± <$g> e)

_âˆ˜Interp_ : âˆ€ {G : Graph â„“g â„“g'}
              {ğ“’ : Category â„“c â„“c'}
              {ğ““ : Category â„“d â„“d'}
              (F : Functor ğ“’ ğ““)
              (Ä± : Interp G ğ“’)
              â†’ Interp G ğ““
F âˆ˜Interp Ä± = Ä± â‹†Interp F

module _ {G : Graph â„“g â„“g'} {ğ“’ : Category â„“c â„“c'}{ğ““ : Category â„“d â„“d'} where
  -- whiskering of an interprIso with a functor
  _âˆ˜Ê³Interp_ : âˆ€ (K : Functor ğ“’ ğ““) {Ä± Ä±' : Interp G ğ“’} â†’ (Î± : InterpIso G ğ“’ Ä± Ä±')
        â†’ InterpIso G ğ““ (K âˆ˜Interp Ä±) (K âˆ˜Interp Ä±')
  (K âˆ˜Ê³Interp Î±) .fst .fst v = K âŸª Î± .fst .fst v âŸ«
  (K âˆ˜Ê³Interp Î±) .fst .snd e = preserveCommF {F = K}(Î± .fst .snd e)
  (K âˆ˜Ê³Interp Î±) .snd v = preserveIsosF {F = K} (isIsoInterpIso _ _ Î± v) .snd

module _ {G : Graph â„“g â„“g'}{H : Graph â„“h â„“h'} {ğ“’ : Category â„“c â„“c'} where
  -- whiskering of an interpiso with a homomorphism
  _âˆ˜Ë¡Interp_ : âˆ€ {Ä± Ä±' : Interp H ğ“’} (Î± : InterpIso H ğ“’ Ä± Ä±') (Ï• : GraphHom G H)
             â†’ InterpIso G ğ“’ (Ä± âˆ˜GrHom Ï•) (Ä±' âˆ˜GrHom Ï•)
  (Î± âˆ˜Ë¡Interp Ï•) .fst .fst v = Î± .fst .fst (Ï• $g v)
  (Î± âˆ˜Ë¡Interp Ï•) .fst .snd e = Î± .fst .snd (Ï• <$g> e)
  (Î± âˆ˜Ë¡Interp Ï•) .snd v = Î± .snd (Ï• $g v)
