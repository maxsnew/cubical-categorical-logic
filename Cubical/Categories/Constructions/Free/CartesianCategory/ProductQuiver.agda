{-# OPTIONS --safe #-}
module Cubical.Categories.Constructions.Free.CartesianCategory.ProductQuiver
  where

open import Cubical.Foundations.Prelude

private variable ℓ ℓ' : Level

module _ (ob : Type ℓ) where
  data ProdExpr : Type ℓ where
    ↑_ : ob → ProdExpr
    _×_ : ProdExpr → ProdExpr → ProdExpr
    ⊤ : ProdExpr
  record ProductQuiver ℓ' : Type (ℓ-suc (ℓ-max ℓ ℓ')) where
    field
      mor : Type ℓ'
      dom : mor → ProdExpr
      -- atomic function symbols are only at base types, WLOG, since a function
      -- symbol at a product type is "just" a finite number of paired function
      -- symbols
      cod : mor → ob

×Quiver : ∀ ℓ ℓ' → Type (ℓ-suc (ℓ-max ℓ ℓ'))
×Quiver ℓ ℓ' = Σ[ ob ∈ Type ℓ ] ProductQuiver ob ℓ'

module ×QuiverNotation (Q : ×Quiver ℓ ℓ') where
  open ProductQuiver (Q .snd) public
  Ob = ProdExpr (Q .fst)
  Dom = dom
  Cod = cod
