{-# OPTIONS --safe #-}
{- Algebras of a monad formulated in terms of extension systems -}
module Cubical.Categories.Monad.Algebra where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Data.Sigma
open import Cubical.Categories.Category
open import Cubical.Categories.Functor renaming (ğŸ™âŸ¨_âŸ© to funcId)
open import Cubical.Categories.NaturalTransformation.Base
open import Cubical.Categories.NaturalTransformation.Properties
open import Cubical.Categories.Functors.HomFunctor
open import Cubical.Categories.Constructions.BinProduct
open import Cubical.Categories.Monad.ExtensionSystem
open import Cubical.Categories.Adjoint.UniversalElements

private
  variable
    â„“ â„“' : Level

module _ {C : Category â„“ â„“'} (M : ExtensionSystem C) where
  open Category
  T = M .fst
  open ExtensionSystemFor (M .snd)

  record AlgebraFor (a : C .ob) : Type (â„“-max â„“ â„“') where
    field
      bindA : âˆ€ {b} â†’ C [ b , a ] â†’ C [ T b , a ]
      bindA-l : âˆ€ {b}{f : C [ b , a ]} â†’ bindA f âˆ˜âŸ¨ C âŸ© Î· â‰¡ f
      bindA-comp : âˆ€ {b c} {f : C [ b , a ]}{g : C [ c , T b ]}
        â†’ bindA f âˆ˜âŸ¨ C âŸ© bind g â‰¡ bindA (bindA f âˆ˜âŸ¨ C âŸ© g)

  open AlgebraFor

  isAlgebraHom : âˆ€ {a b} â†’ AlgebraFor a â†’ AlgebraFor b â†’ (C [ a , b ]) â†’ Type _
  isAlgebraHom Î± Î² Ï• =
    âˆ€ {c} (f : C [ c , _ ]) â†’ Ï• âˆ˜âŸ¨ C âŸ© Î± .bindA f â‰¡ Î² .bindA (Ï• âˆ˜âŸ¨ C âŸ© f)

  isPropIsAlgebraHom : âˆ€ {a b} â†’
                       (Î± : AlgebraFor a)(Î² : AlgebraFor b)
                       (Ï• : C [ a , b ]) â†’ isProp (isAlgebraHom Î± Î² Ï•)
  isPropIsAlgebraHom Î± Î² Ï• = isPropImplicitÎ  Î» _ â†’ isPropÎ  Î» f â†’ C .isSetHom _ _

  Algebra = Î£[ a âˆˆ C .ob ] AlgebraFor a

  AlgebraHom : Algebra â†’ Algebra â†’ Type _
  AlgebraHom Î± Î² =
    Î£[ Ï• âˆˆ C [ Î± .fst , Î² .fst ] ] isAlgebraHom (Î± .snd) (Î² .snd) Ï•

  isSetAlgebraHom : âˆ€ {Î± Î²} â†’ isSet (AlgebraHom Î± Î²)
  isSetAlgebraHom {x}{y} =
    isSetÎ£ (C .isSetHom)
      (Î» Ï• â†’ isPropâ†’isSet (isPropIsAlgebraHom (x .snd) (y .snd) Ï•))

  AlgebraHomâ‰¡ : âˆ€ {Î± Î²} (Ï• Ïˆ : AlgebraHom Î± Î²) â†’ Ï• .fst â‰¡ Ïˆ .fst â†’ Ï• â‰¡ Ïˆ
  AlgebraHomâ‰¡ {Î±}{Î²} Ï• Ïˆ p =
    Î£â‰¡Prop (Î» Ï• â†’ isPropIsAlgebraHom (Î± .snd) (Î² .snd) Ï•) p

  ALGEBRA : Category _ _
  ALGEBRA .ob = Algebra
  ALGEBRA .Hom[_,_] = AlgebraHom
  ALGEBRA .id {x} =
    C .id , Î» f â†’ (C .â‹†IdR _) âˆ™ cong (x .snd .bindA) (sym (C .â‹†IdR _))
  ALGEBRA ._â‹†_ {z = Î³} Ï• Ïˆ =
    (Ï• .fst â‹†âŸ¨ C âŸ© Ïˆ .fst) , (Î» f â†’
    sym (C .â‹†Assoc _ _ _) âˆ™
      congâ‚‚ (C ._â‹†_) (Ï• .snd _) refl âˆ™ Ïˆ .snd _ âˆ™
        cong (Î³ .snd .bindA) (C .â‹†Assoc _ _ _))
  ALGEBRA .â‹†IdL {x}{y} Ï• =
    Î£â‰¡Prop (isPropIsAlgebraHom (x .snd) (y .snd)) (C .â‹†IdL _)
  ALGEBRA .â‹†IdR {x}{y} Ï• =
    Î£â‰¡Prop (isPropIsAlgebraHom (x .snd) (y .snd)) (C .â‹†IdR _)
  ALGEBRA .â‹†Assoc {w}{x}{y}{z} f g h =
    Î£â‰¡Prop (isPropIsAlgebraHom (w .snd) (z .snd)) (C .â‹†Assoc _ _ _)
  ALGEBRA .isSetHom {x}{y} =
    isSetÎ£ (C .isSetHom)
      (Î» Ï• â†’ isPropâ†’isSet (isPropIsAlgebraHom (x .snd) (y .snd) Ï•))

  open Functor

  FreeAlgebra : âˆ€ (a : C .ob) â†’ Algebra
  FreeAlgebra a = T a , free-alg where
    free-alg : AlgebraFor (T a)
    free-alg .bindA = bind
    free-alg .bindA-l = bind-l
    free-alg .bindA-comp = bind-comp

  funToFreeHom : âˆ€ (a b : C .ob) â†’
                 (Kleisli _ M [ a , b ]) â†’
                 AlgebraHom (FreeAlgebra a) (FreeAlgebra b)
  funToFreeHom a b f .fst = bind f
  funToFreeHom a b f .snd g = bind-comp

  Underlying : Functor ALGEBRA C
  Underlying .F-ob x = x .fst
  Underlying .F-hom x = x .fst
  Underlying .F-id = refl
  Underlying .F-seq f g = refl

  open import Cubical.Categories.Presheaf.Representable
  open UnivElt
  open isUniversal
  Free : LeftAdjoint ALGEBRA C Underlying
  Free c .vertex = FreeAlgebra c
  Free c .element = Î·
  Free c .universal .coinduction {Î²} f =
    (Î² .snd .bindA f) , Î» g â†’ Î² .snd .bindA-comp
  Free c .universal .commutes {Î²} f = Î² .snd .bindA-l
  Free c .universal .is-uniq {Î²} Ï• (Ïˆ , Ïˆ-homo) x =
    AlgebraHomâ‰¡ {FreeAlgebra _}{Î²} ((Ïˆ , Ïˆ-homo))
      ((Î² .snd .bindA Ï•) , Î» g â†’ Î² .snd .bindA-comp)
    ( (sym (C .â‹†IdL _) âˆ™ congâ‚‚ (seq' C) (sym bind-r) refl)
    âˆ™ Ïˆ-homo Î·
    âˆ™ cong (Î² .snd .bindA) x)
