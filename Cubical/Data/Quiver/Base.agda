-- A Quiver is an endo-span of types.
--   ob <- mor -> ob
-- This is often used in set theory as the data over which a category
-- is defined to be algebraic structure.

-- A Quiver is equivalent to a Graph (modulo universe issues), but
-- they are not definitionally isomorphic: turning a quiver into a
-- graph introduces a Path between objects/nodes in the definition of
-- an Edge.

-- Since avoiding Paths generally leads to cleaner code, Graphs or
-- Quivers may be preferable depending on the variance of a
-- construction.

-- 1. *Using* a Graph generally requires fewer paths between
--    objects. For instance, Graphs are preferable to be used in the
--    definition of a category because composition can be defined by
--    sharing a common middle variable Hom[ A , B ] Ã— Hom[ B , C ] â†’
--    Hom[ A , C ], which in a Quiver would need a path (e e' : mor) â†’
--    (cod e â‰¡ dom e') â†’ mor.
--
-- 2. *Producing* a Quiver generally requires fewer paths between
--    objects. For instance, Quivers are preferable to be used in the
--    definition of generating data for free and presented categories.
--    As an example, the "Funny tensor product" C âŠ— D of categories is
--    defined by generators and relations. The generators are easy to
--    define as a Quiver, but if defined as a graph, then the
--    generators require a path between objects.

-- So as a principle, to get the most general definitions,
-- 1. *Produce* Graphs
-- 2. *Use* Quivers
-- when you can get away with it.

{-# OPTIONS --safe #-}
module Cubical.Data.Quiver.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Data.Graph
open import Cubical.Data.Sigma
open import Cubical.Categories.Category.Base
open import Cubical.Categories.UnderlyingGraph
open import Cubical.Categories.Displayed.Base

private
  variable
    â„“c â„“c' â„“cá´° â„“cá´°' â„“d â„“d' â„“g â„“g' â„“gá´° â„“gá´°' â„“h â„“h' â„“j â„“ : Level

-- Useful in certain applications to separate this into components
record QuiverOver (ob : Type â„“g) â„“g' : Type (â„“-suc (â„“-max â„“g â„“g')) where
  field
    mor : Type â„“g'
    dom : mor â†’ ob
    cod : mor â†’ ob

open QuiverOver
Quiver : âˆ€ â„“g â„“g' â†’ Type _
Quiver â„“g â„“g' = Î£[ ob âˆˆ Type â„“g ] QuiverOver ob â„“g'

-- A "heteromorphism" from a Quiver to a Graph
record HetQG (Q : Quiver â„“c â„“c') (G : Graph â„“g â„“g')
       : Type (â„“-max (â„“-max â„“c â„“c') (â„“-max â„“g â„“g')) where
  field
    _$g_ : Q .fst â†’ G .Node
    _<$g>_ : (e : Q .snd .mor)
           â†’ G .Edge (_$g_ (Q .snd .dom e)) (_$g_ (Q .snd .cod e))
open HetQG public

Graphâ†’Quiver : Graph â„“c â„“c' â†’ Quiver â„“c (â„“-max â„“c â„“c')
Graphâ†’Quiver g .fst = g .Node
Graphâ†’Quiver g .snd .mor = Î£[ A âˆˆ g .Node ] Î£[ B âˆˆ g .Node ] g .Edge A B
Graphâ†’Quiver g .snd .dom x = x .fst
Graphâ†’Quiver g .snd .cod x = x .snd .fst

-- | The use of â‰¡ in this definition is the raison d'etre for the
-- | Quiver-Graph distinction
Quiverâ†’Graph : Quiver â„“c â„“c' â†’ Graph â„“c (â„“-max â„“c â„“c')
Quiverâ†’Graph Q .Node = Q .fst
Quiverâ†’Graph Q .Edge A B =
  Î£[ e âˆˆ Q .snd .mor ]
    (Q .snd .dom e â‰¡ A)
  Ã— (Q .snd .cod e â‰¡ B)

Catâ†’Quiver : Category â„“c â„“c' â†’ Quiver â„“c (â„“-max â„“c â„“c')
Catâ†’Quiver ğ“’ = Graphâ†’Quiver (Catâ†’Graph ğ“’)

module _ (Q : Quiver â„“g â„“g') where
  -- Of the four possible combinations: Q over Q, G over Q, Q over G,
  -- Q over Q, this strikes the right balance. Having the displayed
  -- thing be a Graph means we can define sections without using â‰¡.
  record Graphá´° â„“gá´° â„“gá´°' : Type (â„“-max (â„“-max â„“g â„“g') (â„“-suc (â„“-max â„“gá´° â„“gá´°')))
         where
    no-eta-equality
    open QuiverOver (Q .snd)
    field
      ob[_] : Q .fst â†’ Type â„“gá´°
      Hom[_][_,_]
        : (e : Q .snd .mor)
        â†’ ob[ Q .snd .dom e ]
        â†’ ob[ Q .snd .cod e ]
        â†’ Type â„“gá´°'

Categoryá´°â†’Graphá´° : {ğ“’ : Category â„“c â„“c'}(ğ“’á´° : Categoryá´° ğ“’ â„“cá´° â„“cá´°')
                 â†’ Graphá´° (Catâ†’Quiver ğ“’) â„“cá´° â„“cá´°'
Categoryá´°â†’Graphá´° ğ“’á´° .Graphá´°.ob[_] x = ğ“’á´° .Categoryá´°.ob[_] x
Categoryá´°â†’Graphá´° ğ“’á´° .Graphá´°.Hom[_][_,_] (A , B , e) Aá´° Bá´° =
  ğ“’á´° .Categoryá´°.Hom[_][_,_] e Aá´° Bá´°

module _ {Q : Quiver â„“c â„“c'} {G : Graph â„“g â„“g'}
       (Ï• : HetQG Q G) (Gá´° : Graphá´° (Graphâ†’Quiver G) â„“gá´° â„“gá´°') where
  open HetQG
  open Graphá´° Gá´°
  reindex : Graphá´° Q â„“gá´° â„“gá´°'
  reindex .Graphá´°.ob[_] q = ob[ Ï• $g q ]
  reindex .Graphá´°.Hom[_][_,_] e A B = Hom[ _ , _ , (Ï• <$g> e) ][ A , B ]

record Section {Q : Quiver â„“g â„“g'} (G : Graphá´° Q â„“gá´° â„“gá´°')
       : Type (â„“-max (â„“-max â„“g â„“g') (â„“-max â„“gá´° â„“gá´°')) where
  open QuiverOver (Q .snd)
  open Graphá´° G
  field
    F-ob  : âˆ€ q â†’ ob[ q ]
    F-hom : âˆ€ e â†’ Hom[ e ][ F-ob (Q .snd .dom e) , F-ob (Q .snd .cod e) ]
